apply plugin: 'com.android.application'
apply plugin: 'android-apt'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "com.abtingramian.abtinblr"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        generatedDensities = []
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // dependency version declarations for interpolation
    def supportLibVer = '24.2.0'
    def supportTestLibVer = '0.5'
    def retrofitLibVer = '2.0.2'
    def okhttpLibVer = '3.3.1'
    def butterknifeLibVer = '8.4.0'
    def gsonLibVer = '2.4'
    def leakcanaryLibVer = '1.5'

    // tests
    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:1.7.1"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "org.robolectric:robolectric:3.0"

    // instrumentation tests
    androidTestCompile "com.android.support.test:runner:${supportTestLibVer}"
    androidTestCompile "com.android.support.test:rules:${supportTestLibVer}"
    androidTestCompile "com.android.support:support-annotations:${supportLibVer}"

    compile "com.android.support:appcompat-v7:${supportLibVer}"
    compile "com.android.support:design:${supportLibVer}"
    compile "com.google.code.gson:gson:${gsonLibVer}"
    compile "com.squareup.retrofit2:retrofit:${retrofitLibVer}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitLibVer}"
    compile "com.squareup.okhttp3:okhttp:${okhttpLibVer}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttpLibVer}"
    compile "com.jakewharton:butterknife:${butterknifeLibVer}"
    apt "com.jakewharton:butterknife-compiler:${butterknifeLibVer}"

    // leak canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakcanaryLibVer}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryLibVer}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryLibVer}"
}
